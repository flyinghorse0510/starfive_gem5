/*
 * Copyright (c) 2022 Starfive
 * All rights reserved
 */

machine(MachineType:D2DNode, "CHI interface to the D2D bridge") :

    // CHI network side channels
    MessageBuffer * reqIn,   network="From", virtual_network="0", vnet_type="none";
    MessageBuffer * snpIn,   network="From", virtual_network="1", vnet_type="none";
    MessageBuffer * rspIn,   network="From", virtual_network="2", vnet_type="none";
    MessageBuffer * datIn,   network="From", virtual_network="3", vnet_type="response";

    MessageBuffer * reqOut,   network="To", virtual_network="0", vnet_type="none";
    MessageBuffer * snpOut,   network="To", virtual_network="1", vnet_type="none";
    MessageBuffer * rspOut,   network="To", virtual_network="2", vnet_type="none";
    MessageBuffer * datOut,   network="To", virtual_network="3", vnet_type="response";

    // CHI bridge side channels
    MessageBuffer * bridge_reqIn;
    MessageBuffer * bridge_snpIn;
    MessageBuffer * bridge_rspIn;
    MessageBuffer * bridge_datIn;

    MessageBuffer * bridge_reqOut;
    MessageBuffer * bridge_snpOut;
    MessageBuffer * bridge_rspOut;
    MessageBuffer * bridge_datOut;

    // Accepted request, but awaiting D2D channel resource
    MessageBuffer * reqRdy;

    // Remap table allocation latency
    int allocation_latency  := 1;

    // Bridge --> NW latencies
    int lat_req_bridge_2_nw := 1;
    int lat_rsp_bridge_2_nw := 1;
    int lat_snp_bridge_2_nw := 1;
    int lat_dat_bridge_2_nw := 1;

    // NW --> Bridge NW latencies
    int lat_req_nw_2_bridge := 1;
    int lat_rsp_nw_2_bridge := 1;
    int lat_snp_nw_2_bridge := 1;
    int lat_dat_nw_2_bridge := 1;

    // Number of REMAP entries
    int num_txremap_entries := 8;
    int num_rxremap_entries := 8;

    int data_channel_size;

    // Src and Dst die Ids
    int src_die_id := 0;
    int dst_die_id := 1;
    
{
    // Cache block size
    int blockSize, default="RubySystem::getBlockSizeBytes()";

    state_declaration(State, default="D2DBridge_State_null") {
        // Null state for debugging
        null,         AccessPermission:Invalid,     desc="Null state";
        stable,       AccessPermission:Invalid,     desc="Stable but invalid, no ongoing transactions";
    }
    
    ////////////////////////////////////////////////////////////////////////////
    // Events
    ////////////////////////////////////////////////////////////////////////////
    enumeration(Event) {
        AllocTxReqEntry,   desc="Allocate a Tx Remap entry";
        AllocRxReqEntry,   desc="Allocate a Rx Remap entry";
        FwdReqMsg,         desc="Fwd to D2DBridge";
        null,              desc="";
        Final,             desc="Finalize event";
    }

    ////////////////////////////////////////////////////////////////////////////
    // Structure
    ////////////////////////////////////////////////////////////////////////////

    structure(TriggerQueue, external ="yes") {
        Event front();
        Event back();
        bool frontNB();
        bool backNB();
        bool empty();
        void push(Event);
        void pushNB(Event);
        void pushFront(Event);
        void pushFrontNB(Event);
        void pop();
        void clear();
    }

    structure(TBE, desc="Common structure for Tx and Rx Table Entry") {
        /* Generated by the originating requestor */
        MachineID o_src_id,       desc="d2d Src Id, Requestor HNF";
        MachineID o_fwd_src_id,   desc="d2d fwd Src Id, Fwd Requestor";
        Addr o_d2d_txn_id,        desc="d2d Txn Id, Original";

        MachineID tx_src_id,      desc="d2d Src Id, generated by src d2d node";
        MachineID rx_src_id,      desc="d2d Src Id, generated by dst d2d node";
        Addr tx_d2d_txn_id,       desc="d2d Txn Id, generated by src d2d node";
        Addr rx_d2d_txn_id,       desc="d2d Txn Id, generated by dst d2d node";
        // Unused fields to appease the SLICC compiler
        TriggerQueue actions,     template="<D2DNode_Event>", desc="List of actions";
        Event slicchack1,         desc="fix compiler not including headers";
    }

    structure(TBETable, external ="yes") {
        TBE lookup(Addr);
        void allocate(Addr);
        void deallocate(Addr);
        bool isPresent(Addr);
    }

    structure(TBEStorage, external ="yes") {
        int size();
        int capacity();
        int reserved();
        int addEntryToNewSlot();
        bool areNSlotsAvailable(int n);
        void incrementReserved();
        void decrementReserved();
    }

    TBETable TxReqMapTable,       template="<D2DNode_TBE>",constructor="m_num_txremap_entries";
    TBEStorage storTxReqMapTable, constructor="this, TBEType_to_string(TBEType_D2DRemap), m_num_txremap_entries";
    
    TBETable RxReqMapTable,       template="<D2DNode_TBE>",constructor="m_num_rxremap_entries";
    TBEStorage storRxReqMapTable, constructor="this, TBEType_to_string(TBEType_D2DRemap), m_num_rxremap_entries";

    ////////////////////////////////////////////////////////////////////////////
    // Interface functions required by SLICC
    ////////////////////////////////////////////////////////////////////////////
    State getState(TBE tbe, Addr txnId) {}

    void setState(TBE tbe, Addr txnId, State state) {}
    
    void setAccessPermission(Addr txnId, State state) {}

    AccessPermission getAccessPermission(Addr txnId) {}

    void functionalRead(Addr txnId, Packet *pkt, WriteMask &mask) {}

    int functionalWrite(Addr txnId, Packet *pkt) {}

    ////////////////////////////////////////////////////////////////////////////
    // Commonly defined functions
    ////////////////////////////////////////////////////////////////////////////
    include "CHI-d2d-funcs.sm";

    ////////////////////////////////////////////////////////////////////////////
    // Used by this module
    ////////////////////////////////////////////////////////////////////////////
    
    TBE getTxReqMapEntry(Addr d2d_txn_id), return_by_pointer="yes" {
        if (TxReqMapTable.isPresent(d2d_txn_id)) {
            return TxReqMapTable.lookup(d2d_txn_id);
        } else {
            return OOD;
        }
    }

    TBE getRxReqMapEntry(Addr d2d_txn_id), return_by_pointer="yes" {
        if (RxReqMapTable.isPresent(d2d_txn_id)) {
            return RxReqMapTable.lookup(d2d_txn_id);
        } else {
            return OOD;
        }
    }

    TBE allocateRxRemapEntry(CHIRequestMsg in_msg), return_by_pointer="yes" {
        storRxReqMapTable.decrementReserved();
        assert(storRxReqMapTable.areNSlotsAvailable(1));

        assert(in_msg.tx_d2d_txn_id == in_msg.rx_d2d_txn_id);
        
        int tbeStorSlot    := storRxReqMapTable.addEntryToNewSlot();
        Addr rx_d2d_txn_id := getD2DTxnId(tbeStorSlot);
        
        RxReqMapTable.allocate(rx_d2d_txn_id);
        TBE tbe           := RxReqMapTable[rx_d2d_txn_id];

        tbe.o_src_id      := in_msg.requestor;
        tbe.o_fwd_src_id  := in_msg.fwdRequestor;
        tbe.o_d2d_txn_id  := in_msg.d2d_txn_id;
        tbe.tx_d2d_txn_id := in_msg.tx_d2d_txn_id;
        tbe.rx_d2d_txn_id := rx_d2d_txn_id;
        tbe.tx_src_id     := in_msg.requestor;
        tbe.rx_src_id     := machineID;
        
        return tbe;
    }

    TBE allocateTxRemapEntry(CHIRequestMsg in_msg), return_by_pointer="yes" {
        storTxReqMapTable.decrementReserved();
        assert(storTxReqMapTable.areNSlotsAvailable(1));

        // Create a new txn_id from the src die
        Addr d2d_txn_id    := in_msg.d2d_txn_id;
        int tbeStorSlot    := storTxReqMapTable.addEntryToNewSlot();
        Addr tx_d2d_txn_id := getD2DTxnId(tbeStorSlot);

        // Allocate a TBE entry
        TxReqMapTable.allocate(tx_d2d_txn_id);
        TBE tbe := TxReqMapTable[tx_d2d_txn_id];

        // Save the original/incoming src_id and txn_id
        tbe.o_src_id      := in_msg.requestor;
        tbe.o_fwd_src_id  := in_msg.fwdRequestor;
        tbe.o_d2d_txn_id  := d2d_txn_id;
        tbe.tx_src_id     := machineID;
        tbe.rx_src_id     := machineID;
        tbe.tx_d2d_txn_id := tx_d2d_txn_id;
        tbe.rx_d2d_txn_id := tx_d2d_txn_id;

        return tbe;
    }

    ////////////////////////////////////////////////////////////////////////////
    // Outbound Ports
    ////////////////////////////////////////////////////////////////////////////
    out_port(network_reqOutPort, CHIRequestMsg,  reqOut);
    out_port(network_snpOutPort, CHIRequestMsg,  snpOut);
    out_port(network_rspOutPort, CHIResponseMsg, rspOut);
    out_port(network_datOutPort, CHIDataMsg,     datOut);

    out_port(reqRdyOutPort, CHIRequestMsg, reqRdy);

    out_port(bridge_reqOutPort, CHIRequestMsg,  bridge_reqOut);
    out_port(bridge_snpOutPort, CHIRequestMsg,  bridge_snpOut);
    out_port(bridge_rspOutPort, CHIResponseMsg, bridge_rspOut);
    out_port(bridge_datOutPort, CHIDataMsg,     bridge_datOut);

    ////////////////////////////////////////////////////////////////////////////
    // Inbound Ports (from NW)
    ////////////////////////////////////////////////////////////////////////////
    in_port(reqInPort, CHIRequestMsg, reqIn, rank=1) {
        if (reqInPort.isReady(clockEdge())) {
            peek(reqInPort, CHIRequestMsg) {
                DPRINTF(RubyCHIDebugStr5,"%s\n",in_msg);
                trigger(Event:AllocTxReqEntry, in_msg.addr, getTxReqMapEntry(in_msg.d2d_txn_id));
            }
        }
    }

    in_port(reqRdyInPort, CHIRequestMsg, reqRdy, rank=2) {
        if (reqRdyInPort.isReady(clockEdge())) {
            peek(reqRdyInPort, CHIRequestMsg) {
                trigger(Event:FwdReqMsg, in_msg.addr, getTxReqMapEntry(in_msg.d2d_txn_id));
            }
        }
    }

    // Inbound Ports (from Bridge)
    in_port(bwReqInPort, CHIRequestMsg, bridge_reqIn, rank=3) {
        if (bwReqInPort.isReady(clockEdge())) {
            peek(bwReqInPort, CHIRequestMsg) {
                // TODO: Think carefully how are you going to access the RxRemap Table
                trigger(Event:AllocRxReqEntry, in_msg.addr, getRxReqMapEntry(in_msg.d2d_txn_id));
            }
        }
    }

    ////////////////////////////////////////////////////////////////////////////
    // Actions
    ////////////////////////////////////////////////////////////////////////////
    action(Alloc_TxReqEntry, desc="") {
        if (storTxReqMapTable.areNSlotsAvailable(1)) {
            storTxReqMapTable.incrementReserved();
            peek(reqInPort, CHIRequestMsg) {
                set_tbe(allocateTxRemapEntry(in_msg));
                enqueue(reqRdyOutPort, CHIRequestMsg, allocation_latency) {
                    out_msg := in_msg;

                }
            }
        } 
        else {
            DPRINTF(RubyCHIDebugStr5, "Retry not implement");
            assert(false);
        }
        reqInPort.dequeue(clockEdge());
    }

    action(Alloc_RxReqEntry, desc="") {
        /**
         * We do not have to send retries
         * for the REQs received from the
         * bridge side, so create a resource
         * stall
         */
        check_allocate(storRxReqMapTable);
        storRxReqMapTable.incrementReserved();
        peek(bwReqInPort, CHIRequestMsg) {
            set_tbe(allocateRxRemapEntry(in_msg));
            enqueue(network_reqOutPort, CHIRequestMsg, lat_req_bridge_2_nw) {
                out_msg               := in_msg;
                out_msg.requestor     := machineID;
                out_msg.fwdRequestor  := machineID;
                // TODO: Complete this after HA-D2D mapping from the address
                // out_msg.Destination.clear(); // Clear the destination from the previous die
                // out_msg.Destination.add();
                out_msg.d2d_txn_id    := tbe.rx_d2d_txn_id;
                out_msg.tx_d2d_txn_id := tbe.tx_d2d_txn_id;
                out_msg.rx_d2d_txn_id := tbe.rx_d2d_txn_id;
            }
        }
        bwReqInPort.dequeue(clockEdge());
    }

    action(Fwd_ReqMsg_ToBridge, desc="") {
        peek(reqRdyInPort, CHIRequestMsg) {
            enqueue(bridge_reqOutPort, CHIRequestMsg, lat_req_nw_2_bridge) {
                assert(is_valid(tbe));
                out_msg := in_msg;
                out_msg.tx_d2d_txn_id := tbe.tx_d2d_txn_id;
                out_msg.rx_d2d_txn_id := tbe.rx_d2d_txn_id;
            }
        }
        reqRdyInPort.dequeue(clockEdge());
    }

    ////////////////////////////////////////////////////////////////////////////
    // Transitions
    ////////////////////////////////////////////////////////////////////////////
    transition(stable, AllocTxReqEntry) {
        Alloc_TxReqEntry;
    }

    transition(stable, AllocRxReqEntry) {
        Alloc_RxReqEntry;
    }

    transition(stable, FwdReqMsg) {
        Fwd_ReqMsg_ToBridge;
    }


}